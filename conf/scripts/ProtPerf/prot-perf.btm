

## Measures the time for down- and up-messages for each protocol individually
## JIRA: https://issues.redhat.com/browse/JGRP-2640


RULE DiagnosticHandler creation
CLASS DiagnosticsHandler
HELPER org.jgroups.tests.helpers.ProtPerfHelper
METHOD <init>
AT EXIT
BIND diag=$this
IF TRUE
   DO diagCreated(diag);
ENDRULE


## If they're is an up_prot, compute the time for it from the header, then set the current time in the header
RULE down(Message)
CLASS ^Protocol
HELPER org.jgroups.tests.helpers.ProtPerfHelper
METHOD down(Message)
AT ENTRY
BIND msg=$1, prot=$0;
IF TRUE
   DO downTime(msg, prot);
ENDRULE



RULE down(Message) completed in TP
CLASS ^TP
HELPER org.jgroups.tests.helpers.ProtPerfHelper
METHOD down(Message)
AT EXIT
BIND msg=$1, cl=$this.getClass();
IF TRUE
   DO computeDownStartTime(msg, cl);
ENDRULE


RULE TP: set up-time for a looped-back message
CLASS ^TP
HELPER org.jgroups.tests.helpers.ProtPerfHelper
METHOD loopback
AT ENTRY
BIND msg=$1, prot=$this;
IF TRUE
   DO upTime(msg, prot);
ENDRULE

RULE TP: set up-time for a single message
CLASS ^SubmitToThreadPool
HELPER org.jgroups.tests.helpers.ProtPerfHelper
METHOD process(Message,boolean)
AT ENTRY
BIND msg=$1, prot=$this.getTransport();
IF TRUE
   DO upTime(msg, prot);
ENDRULE

RULE TP: set up-time for a message batch
CLASS ^SubmitToThreadPool
HELPER org.jgroups.tests.helpers.ProtPerfHelper
METHOD process(MessageBatch,boolean)
AT ENTRY
BIND batch=$1, prot=$this.getTransport();
IF TRUE
   DO upTime(batch, prot);
ENDRULE


RULE up(Message)
CLASS ^Protocol
HELPER org.jgroups.tests.helpers.ProtPerfHelper
METHOD up(Message)
AT ENTRY
BIND msg=$1, prot=$this;
IF TRUE
   DO upTime(msg, prot);
ENDRULE



RULE up(MessageBatch)
CLASS ^Protocol
HELPER org.jgroups.tests.helpers.ProtPerfHelper
METHOD up(MessageBatch)
AT ENTRY
BIND batch=$1, prot=$0;
IF TRUE
   DO upTime(batch, prot);
ENDRULE


RULE ProtocolStack.up(Message)
CLASS ProtocolStack
HELPER org.jgroups.tests.helpers.ProtPerfHelper
METHOD up(Message)
AT EXIT
BIND msg=$1, prot=$this;
IF TRUE
   DO upTime(msg, prot);
ENDRULE


RULE ProtocolStack.up(MessageBatch)
CLASS ProtocolStack
HELPER org.jgroups.tests.helpers.ProtPerfHelper
METHOD up(MessageBatch)
AT EXIT
BIND batch=$1, prot=$this;
IF TRUE
   DO upTime(batch, prot);
ENDRULE